#!/usr/bin/env zsh
# vim: set foldmethod=marker filetype=zsh :

local SSH_ENV="$HOME/.ssh/environment"

__known_agent_running() {
  (( ${+SSH_AGENT_PID} )) && kill -0 $SSH_AGENT_PID
}

__any_agent_running() {
  ssh-add -l &>/dev/null
  if [[ "$?" == 2 ]]; then  # No Agent running
    return 1
  fi
  return 0
}

ssh-agent-running() {
  __known_agent_running || __any_agent_running
}


if ! ssh-agent-running; then
  if [[ -r "$SSH_ENV" ]]; then
    eval "$(< "$SSH_ENV")" >/dev/null
  fi

  if ! ssh-agent-running; then  # Still cannot connect to agent
    (umask 066; ssh-agent -s -t 3600 > "$SSH_ENV")
    eval "$(< "$SSH_ENV")" >/dev/null
  fi
fi
echo -e "\n  $(tput setaf 80; tput bold)ssh-agent$(tput sgr0)  --  PID: ${SSH_AGENT_PID}\n"
