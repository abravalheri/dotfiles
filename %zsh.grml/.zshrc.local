#!/usr/bin/env zsh
# Fix terminal colors
local __dircolors_config=~/.config/dircolors/dircolors.solarized-256-dark
if [ -f "${__dircolors_config}" ]; then
  eval "$(dircolors "${__dircolors_config}")"
fi
unset __dircolors_config

# Adjust PATH
export -U PATH="$HOME/.local/bin:$PATH"
export -U FPATH="$XDG_CONFIG_HOME/zshrc.d/autoloaded/:$FPATH"

# Imports
# Source configurations from extra files {{{
function __zshrc_load_extra() {
  local -U extra priv list
  local fp
  extra=(
    # Meant for external installations, just check if file exists:
    /usr/share/autojump/autojump.zs[h](N)  # [h] -> POG to activate glob

    # Check if the file is executable (so we can deactivate it with chmod)
    $HOME/.config/zsh/+local*.zsh(xN)
    # ^  Files specific to host, not shared via dotfiles
    $HOME/.config/zshrc.d/?*.zsh(xN)
    # ^  Files from other program configurations (e.g. TMUX)
  )
  for fp in $extra; source "$fp"
}
__zshrc_load_extra
unset -f __zshrc_load_extra  # avoid lambdas so zprof can be used
# }}}

# SSH/GPG Agents
ssh-init

# VIM MODE
function zle-line-init zle-keymap-select {
    VIM_PROMPT="%F{yellow}-- NORMAL --%f"
    RPS1="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/} $EPS1"
    zle reset-prompt
}

bindkey -v

bindkey '^P' up-history
bindkey '^N' down-history
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word
bindkey '^r' history-incremental-search-backward

zle -N zle-line-init
zle -N zle-keymap-select
export KEYTIMEOUT=1

export -U PATH FPATH
